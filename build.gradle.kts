import io.spring.gradle.dependencymanagement.org.codehaus.plexus.interpolation.os.Os.FAMILY_MAC
import org.apache.tools.ant.taskdefs.condition.Os
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.springframework.boot.gradle.tasks.bundling.BootJar

val valtimoVersion: String by project
val ktlintVersion: String by project
val ktlintToolVersion: String by project

plugins {
    // Idea
    idea
    id("org.jetbrains.gradle.plugin.idea-ext")

    // Spring
    id("org.springframework.boot")
    id("io.spring.dependency-management")

    // Kotlin
    kotlin("jvm")
    kotlin("plugin.spring")
    kotlin("plugin.jpa")
    kotlin("plugin.allopen")

    // Checkstyle
    id("org.jlleitschuh.gradle.ktlint")

    // Other
    id("com.avast.gradle.docker-compose")
    id("cn.lalaki.central")
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = uri("https://s01.oss.sonatype.org/content/repositories/releases/") }
        maven { url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/") }
        maven { url = uri("https://valtimo-releases.s3.eu-central-1.amazonaws.com/") }
    }
}

subprojects {
    println("Configuring ${project.path}")

    if (project.path.startsWith(":backend")) {

        tasks.withType<org.springframework.boot.gradle.tasks.run.BootRun> {
            mainClass.set("com.ritense.plugin.sandbox.PluginApplication")
        }
        apply(plugin = "java")
        apply(plugin = "org.springframework.boot")
        apply(plugin = "io.spring.dependency-management")

        apply(plugin = "idea")
        apply(plugin = "java-library")
        apply(plugin = "kotlin")
        apply(plugin = "kotlin-spring")
        apply(plugin = "kotlin-jpa")
        apply(plugin = "com.avast.gradle.docker-compose")
        apply(plugin = "maven-publish")
        apply(plugin = "org.jlleitschuh.gradle.ktlint")

        java.sourceCompatibility = JavaVersion.VERSION_21
        java.targetCompatibility = JavaVersion.VERSION_21

        tasks.withType<KotlinCompile> {
            compilerOptions {
                jvmTarget = JvmTarget.JVM_21
                javaParameters = true
            }
        }

        dependencies {
            compileOnly(platform("com.ritense.valtimo:valtimo-dependency-versions:$valtimoVersion"))
            testImplementation(platform("com.ritense.valtimo:valtimo-dependency-versions:$valtimoVersion"))
            // cn.lalaki.central is a publishing *plugin*, resolved through pluginManagement in
            // settings.gradle.kts. It used to be declared here as an implementation dependency
            // too, which put it in the published pom as a runtime dependency - every consumer
            // of this plugin pulled a Gradle publishing tool at runtime. Nothing in the source
            // references it.
        }

        allOpen {
            annotation("com.ritense.valtimo.contract.annotation.AllOpen")
        }

        java {
            withSourcesJar()
            withJavadocJar()
        }

        if (Os.isFamily(FAMILY_MAC)) {
            println("Configure docker compose for macOs")
            dockerCompose {
                // No projectNamePrefix: setProjectName below takes precedence over it, and
                // each module overrides that again (imap-mail, imap-mail-dev), so the prefix
                // was dead configuration left over from the plugin template.
                setProjectName("${rootProject.name}-${project.name}")
                executable = "/usr/local/bin/docker-compose"
                dockerExecutable = "/usr/local/bin/docker"
            }
        }

        tasks.getByName<BootJar>("bootJar") {
            enabled = false
        }

        apply(from = "$rootDir/gradle/test.gradle.kts")
        apply(from = "$rootDir/gradle/plugin-properties.gradle.kts")
        val pluginProperties = extra["pluginProperties"] as Map<*, *>

        tasks.jar {
            enabled = true

            // Spring Boot's plugin classifies the ordinary jar as `-plain`, because normally
            // bootJar owns the empty classifier. bootJar is disabled here (these are
            // libraries, not applications), so without this the publication has no main
            // artifact at all: the pom comes out as <packaging>pom</packaging> and a Maven
            // consumer of com.ritense.valtimoplugins:imap-mail gets no code.
            archiveClassifier.set("")

            manifest {
                pluginProperties["pluginArtifactId"]?.let { attributes["Implementation-Title"] = it }
                pluginProperties["pluginVersion"]?.let { attributes["Implementation-Version"] = it }
            }
        }
    }
    if (project.path.startsWith(":backend") && project.name != "app" && project.name != "gradle" && project.name != "backend") {
        apply(from = "$rootDir/gradle/publishing.gradle")
    }
}

ktlint {
    version.set(ktlintToolVersion)
}

tasks.bootJar {
    enabled = false
}

println("Configuring has finished")
